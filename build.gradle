plugins {
    id "viaproxy.plugin-conventions"
    id "net.lenni0451.repackager" version "1.0.2"
}

configurations {
    repackagedIncludeInJar

    includeInJar.extendsFrom(repackagedIncludeInJar)
}

repositories {
    maven {
        name = "DirtPowered Releases"
        url = "https://repo.mzga.dev/releases"
    }
    maven {
        name = "DirtPowered Snapshots"
        url = "https://repo.mzga.dev/snapshots"
    }
}

dependencies {
    implementation("net.raphimc:ViaProxy:3.4.8") {
        transitive = false
    }

    repackagedIncludeInJar("dev.mzga:Beta2Release:1.0.6-SNAPSHOT") {
        transitive = false
    }
}

dependencyRepackager {
    configuration = configurations.repackagedIncludeInJar
    relocations = [
            "org.apache.commons"     : "com.github.dirtpowered.betatorelease.libs.org.apache.commons",
            "net.lenni0451.mcstructs": "com.github.dirtpowered.betatorelease.libs.net.lenni0451.mcstructs",
            "com.google"             : "com.github.dirtpowered.betatorelease.libs.com.google"
    ]
    removals = [
            "org/apache/logging/log4j",
            "io/netty",
            "META-INF/org/apache/logging/log4j",
            "META-INF/versions/9/org/apache/logging/log4j",
            "META-INF/services/org.apache.logging.log4j",
            "log4j"
    ]
    removeEmptyDirs = true
}
